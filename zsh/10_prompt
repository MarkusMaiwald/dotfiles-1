setopt prompt_subst

function prompt-vagrant() {
  if [[ -f ".vagrant" ]]; then
    echo $PROMPT_VAGRANT;
  fi
}

function prompt-git() {
  if git rev-parse --git-dir > /dev/null 2>&1; then
    echo "$(prompt-git-branch)$(prompt-git-dirty)$(prompt-git-tracking)"
  fi
}

function prompt-git-branch() {
  echo "$PROMPT_GIT_PROMPT_PREFIX$(git-current-branch)$PROMPT_GIT_PROMPT_SUFFIX"
}

function prompt-git-dirty() {
  if [[ -n $(git status -s 2> /dev/null) ]]; then
    echo "$PROMPT_GIT_PROMPT_DIRTY"
  fi
}

function prompt-git-tracking() {
  if git rev-parse @{upstream} > /dev/null 2>&1; then
    behind=$(git-commits-behind)
    ahead=$(git-commits-ahead)
    if [[ $behind -gt 0 && $ahead -gt 0 ]]; then
      echo "$PROMPT_GIT_PROMPT_DIVERGED"
    elif [[ $behind -gt 0 ]]; then
      echo "$PROMPT_GIT_PROMPT_BEHIND"
    elif [[ $ahead -gt 0 ]]; then
      echo "$PROMPT_GIT_PROMPT_AHEAD"
    fi
  else
    echo $PROMPT_GIT_NO_TRACK
  fi
}

PROMPT_GIT_PROMPT_PREFIX="%F{yellow}"
PROMPT_GIT_PROMPT_SUFFIX="%f"
PROMPT_GIT_PROMPT_DIRTY=" %F{red}✶%f"
PROMPT_GIT_PROMPT_CLEAN=""
PROMPT_GIT_PROMPT_AHEAD=" %F{green}→%f"
PROMPT_GIT_PROMPT_BEHIND=" %F{red}←%f"
PROMPT_GIT_PROMPT_DIVERGED=" %F{yellow}⇆%f"
PROMPT_GIT_NO_TRACK=" %F{red}⚡%f"
PROMPT_VAGRANT="❒ "

PROMPT='%F{blue}%2c%f '
RPROMPT='%F{blue}$(prompt-vagrant)$(prompt-git)'
